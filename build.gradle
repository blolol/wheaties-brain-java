plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '6.1.0'
    id 'com.palantir.docker' version '0.25.0'
}

group 'com.blolol'
version '1.0.0'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.pircbotx:pircbotx:2.1'
    implementation 'org.slf4j:slf4j-api:1.7.30'
    implementation 'org.slf4j:slf4j-simple:1.7.30'
    implementation files('libs/JMegaHal.jar')
}

jar {
    manifest {
        attributes([
            'Implementation-Title': project.name,
            'Implementation-Version': project.version,
            'Implementation-Vendor': 'Blolol',
            'Main-Class': 'com.blolol.wheaties.WheatiesMain'
        ])
    }
}

shadowJar {
    // Output to build/libs/wheaties-brain-java-${BUILD_VERSION}.jar
    // https://imperceptiblethoughts.com/shadow/configuration/#configuring-output-name
    archiveClassifier.set('')
}

docker {
    name "blolol/${project.name}:${project.version}"
    tag 'latest', "blolol/${project.name}:latest"
    buildArgs([
        BUILD_VERSION: project.version
    ] as Map<String, String>)

    // Copy files from build/libs into build/docker, which is used as the Docker build context
    // https://github.com/palantir/gradle-docker#docker-plugin
    copySpec.from('build/libs').into('build/libs')
}
